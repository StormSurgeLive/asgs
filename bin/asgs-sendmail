#!/usr/bin/env perl
#--------------------------------------------------------------------------
# asgs-sendmail
#--------------------------------------------------------------------------
# Copyright(C) 2019 Brett Estrade
# Copyright(C) 2019 Jason Fleming
#
# This file is part of the ADCIRC Surge Guidance System (ASGS).
#
# The ASGS is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# ASGS is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with the ASGS.  If not, see <http://www.gnu.org/licenses/>.
#--------------------------------------------------------------------------
package bin::asgs_sendmail;

use strict;
use warnings;

use Email::Sender::Simple qw(sendmail);     # main email capability (receives TLS obj)
use Email::Sender::Transport::SMTP::TLS;    # creates transport object
use Email::Simple::Creator;                 # generates message itself (converts header and body from hash to text email)
use Config::Tiny;                           # reads ini format ... converts to a perl hash
use Try::Tiny;                              # exception handling (alternative to eval)
use POSIX qw/strftime/;
use Getopt::Long;
use Util::H2O::More qw/h2o ini2h2o/;
use constant EXIT_SUCCESS => 0;

my $subject     = q{no subject};
my $HOME        = ( getpwuid($<) )[7];
my $config_file = qq{$HOME/asgs-global.conf};

# $logfile default, assumes asgsh env, which has $SCRIPTDIR defined
# so ideal default $logfile is /path/to/asgs/mail.log
my $logfile = sprintf( qq{%s/mail.log}, $ENV{SCRIPTDIR} // $HOME );
my $to;

GetOptions(
    "c|config=s"  => \$config_file,
    "h|help"      => \&show_help,
    "l|logfile=s" => \$logfile,
    "s|subject=s" => \$subject,
    "t|to=s"      => \$to,
);

# verify recipient email has been set via --to
die qq{a recipient is required to be defined via --to\n} unless defined $to;

# make sure that $to is a comma delimited list of email addresses, accept a
# space delimted list, but reform it to use commas

$to =~ s/,/ /g;             # turn all commas to spaces
$to =~ s/ +/ /g;            # reduce all contiguous spaces of 2 or more, down to 1 space
my @to = split / /, $to;    # split on single space
$to = join q{,}, @to;       # recreate list of email addresses and delimit them by commas

exit run();

sub run {
    my @content = <STDIN>;
    my $content;
    if (@content) {

        # don't need to add a newline between records since each already
        # ends in a newline
        $content = join( qq{}, @content );
    }
    else {
        $content = q{empty message};
    }

    my $config = ini2h2o $config_file;

    # create log message
    my $logmsg = sprintf( qq{subject: "%s", to: "%s"}, $subject, $to );

    # WAL
    logger( type => q{WAL}, file => $logfile, msg => $logmsg );

    my $transport = try {
        Email::Sender::Transport::SMTP::TLS->new(
            host     => $config->email->smtp_host,
            port     => $config->email->smtp_port // 587,    # defaults to TLS if not set
            username => $config->email->smtp_username,
            password => $config->email->smtp_password,
            helo     => 'HELO',
        );
    }
    catch {
        logger( $logfile, qq{(FAILED) $logmsg - $_} );
        logger( type => q{FAILED}, file => $logfile, msg => qq{$logmsg - $_} );
        die "Error sending email: $_";
    };

    # check for subjects > 78 characters, which is the max line length according to the
    # email RFC
    if ( length $subject > 99 ) {
        warn qq{(asgs-sendmail) WARNING: Length of --subject is > 99 characters, truncating ...\n};
        $subject = substr $subject, 0, 99;
    }

    my $message = Email::Simple->create(
        header => [
            'Reply-To' => $config->email->reply_to_address // $config->email->from_address,
            From       => $config->email->from_address,
            To         => $to,
            Subject    => $subject,
        ],
        body => $content,
    );

    try {
        sendmail( $message, { transport => $transport } );
        logger( type => q{OK}, file => $logfile, msg => $logmsg );
    }
    catch {
        logger( type => q{FAIL}, file => $logfile, msg => qq{$logmsg - $_} );
        die "Error sending email: $_";
    };
    return EXIT_SUCCESS;
}

sub show_help {
    print qq{$0 --subject "subject line" --to recipient\@email.tdl < body.txt\n};
    exit EXIT_SUCCESS;
}

sub logger {
    my %opts = @_;
    my $opts = h2o \%opts;    # make accessors
    if ( open my $fh, q{>>}, $opts->file ) {
        my $timestamp = strftime( "%a %b %e %H:%M:%S %Y UTC", gmtime(time) );
        printf $fh qq{%06d (%4s) [%s] %s\n}, $$, $opts->type, $timestamp, $opts->msg;
        close $fh;
    }
}

1;

__END__

=head1 NAME

  asgs-sendmail.pl

=head1 USAGE 

  # piped STDIN
  cat body.txt | asgs-sendmail.pl [-c override/default/conf] --subject "subject line" --to recipient@email.tld
  some-utility | asgs-sendmail.pl [-c override/default/conf] --subject "subject line" --to recipient@email.tld

  # directed file contents in using '<', typical unix syntax
  asgs-sendmail.pl [-c override/default/conf] --subject "subject line" --to recipient@email.tld < body.txt

=head2 Multiple Recipients

The C<--to> options accepts a comma delimited list of email addresses:

  some-utility | asgs-sendmail.pl [-c override/default/conf] --subject "subject line" --to "recipient1@email.tld, recipient2@email.tld, ..., recipientN@email.tld"

A list of email addresses that are separated by spaces (like the actual sendmail utility) is also supported

  some-utility | asgs-sendmail.pl [-c override/default/conf] --subject "subject line" --to "recipient1@email.tld recipient2@email.tld ... recipientN@email.tld"

=head1 DESCRIPTION

This utility is used to send emails using an external SMTP server.

=head1 OPTIONS

=head2 Required

=over 4

=item C<-t|--to>

One or more recipients must be defined. If more than one, pass in as a comma delimted string. See perldoc for more information (POD at the end of this file).

=back

=head2 Optional

=over 4

=item C<-c|--config path/to/config.file>

Define a non-default configuration file. See below for the format involved in creating the configuration file used by this script.

=item C<-l|--logfile path/to/log.file>

Specifies a log file; assumes C<ASGS Shell Environment>, thus it
defaults to C<$ENV{SCRIPTDIR}/mail.log>; if this environment variable is
not defined, then it default to C<$HOME/mail.log>.

=item C<-s|--subject>

Defines subject thread. There is a default, but it's meaningless.

If the subject test is > 99 characters in length, the script issues a warning
and truncates the subject text. This limit was determined through empiracle
testing of what provided for the most reliable email delivery.

=back

=head1 CONFIGURATION

By default, a configuration file located at C<$HOME/asgs-global.conf>
is required. This file contains the necessary authoriziation information
required for sending emails through an external SMTP server gateway.
TLS (port 587) is a preferred transport for security reasons, but the
underlying module supports SSL and unecrypted (port 25).

  [email]
  from_address=info@stormsurge.email
  mail_reply_to=info@stormsurge.email
  smtp_host=smtp.host.name
  smtp_port=587 
  smtp_username=smtp-username
  smtp_password=smtp-password

=head1 LIMITATIONS

Currently, the content of the body of the message must be provided
via STDIN, this means it must be piped in via an external source.
This source could be another program or an existing file.
